description torque (
    kind = 'main' and
    short = 'Install and configure a cluster TORQUE from distribution repositories.' and
    content = 'The template installs TORQUE from the package repositories. Also it creates an user "user1", which is common to all nodes and can be used to launch jobs. Initially the template creates as many working node hostnames as the sum of the values of feature "ec3_max_instances_max" in every system.

Webpage: http://www.adaptivecomputing.com/products/open-source/torque'
)

network public (
  outbound = 'yes' and
  outports contains '15001/tcp' and
  outports contains '15001/udp' and
  outports contains '15002/tcp' and
  outports contains '15002/udp' and
  outports contains '15003/tcp' and
  outports contains '15003/udp' and
  outports contains '15004/tcp' and
  outports contains '15004/udp' and
  outports contains '1023/tcp' and
  outports contains '1023/udp' and
  # Im REST port
  outports contains '8800/tcp'
)
network private ()

system front (
  # Net interfaces definition. Order matters !
  net_interface.0.connection = 'private' and
  net_interface.0.dns_name = 'torqueserver' and
  net_interface.1.connection = 'public' and
  net_interface.1.dns_name = 'torqueserverpublic' and
  queue_system = 'torque' and
  ec3_templates contains 'torque' and
  # Install ansible roles needed
  disk.0.applications contains (name = 'ansible.modules.grycap.torque') and
  disk.0.applications contains (name = 'ansible.modules.grycap.clues') and
  disk.0.applications contains (name = 'ansible.modules.grycap.im')
)

configure front (
@begin
---
  - vars:
      SYSTEMS:
        ec3_jpath: /system/*
      NNODES: '{{ SYSTEMS | selectattr("ec3_max_instances_max", "defined") | sum(attribute="ec3_max_instances_max") }}'
      # Variables needed for CLUES
      AUTH:
        ec3_xpath: /system/front/auth

    roles:
    # The install order matters !
    # If the role order is altered the installation won't work
    - { role: 'grycap.im' }
    - { role: 'grycap.torque', torque_type_of_node: 'front',
                               max_number_of_nodes: '{{ NNODES }}' }
    - { role: 'grycap.clues', auth: '{{AUTH}}',
                              clues_queue_system: 'torque',
                              max_number_of_nodes: '{{ NNODES }}',
                              vnode_prefix: 'wn' }
@end
)

system wn (
      net_interface.0.connection='private'
)

configure wn (
@begin
---
  - roles:
    - { role: 'grycap.torque', torque_type_of_node: 'wn' }
@end
)

include torque_misc (
  template = 'openports'
)

deploy front 1
